
import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask

buildscript {
    ext {
        protobufVersion = '3.19.3'
        protobufPluginVersion = '0.8.18'
        grpcVersion = '1.43.1'
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
    // https://github.com/davidmc24/gradle-avro-plugin/blob/master/examples/avsc-from-subproject/cat/build.gradle
    // automatically detects avsc files in src/main
    id "com.github.davidmc24.gradle.plugin.avro-base" version "1.3.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "org.apache.avro:avro:1.11.0"

    // required for generated grcp class annotations
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

def generateAvro = tasks.register("generateAvro", GenerateAvroJavaTask) {
    source("$projectDir/src/main/avro")
    outputDir = file("$projectDir/src/generated/main/avro")
}

tasks.named("compileJava").configure {
    source(generateAvro)
}
